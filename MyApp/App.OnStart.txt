Set(AppPublishedVersion, "0.0.2");

If(
    !AppInitialized
    ,
    
    // 初期化処理

    // グローバル変数宣言

    // Dataverseから設定読み出し
    Set(KeySolution, "Yakata");
    Set(KeyApp, "MyApp");

    Refresh(Logs);
    Refresh(LogLevels);
    Refresh(Resources);
    Refresh(Settings);
    Refresh(SettingTypes);
    Refresh(Strings);
    
    Set(AppAdministrators,         Filter(Settings,       Key=KeyApp & "/User/Administrators"));
    Set(AppAuthor,                 LookUp(Settings,       Key=KeyApp & "/Author").Value);
    Set(BlueLightCutMode,          If(LookUp(Settings,    Key=KeyApp & "/BlueLightCutMode").Value = "true", true, false));
    Set(DebugMode,                 If(LookUp(Settings,    Key=KeyApp & "/DebugMode").Value = "true", true, false));
    Set(MaintenanceMode,           If(LookUp(Settings,    Key=KeyApp & "/MaintenanceMode").Value = "true", true, false));
    Set(AppId,                     LookUp(Settings,       Key=KeyApp & "/Id").Value); // Set(AppId, GetAppIdMyApp.Run().appid);
    Set(AppVersion,                LookUp(Settings,       Key=KeyApp & "/Version").Value);
    // アプリごとの設定がなければソリューション全体の設定で上書き
    If(IsEmpty(AppAdministrators), Set(AppAdministrators, Filter(Settings,    Key=KeySolution & "/User/Administrators")));
    If(IsBlank(AppAuthor),         Set(AppAuthor,         LookUp(Settings,    Key=KeySolution & "/Author").Value));
    If(IsBlank(BlueLightCutMode),  Set(BlueLightCutMode,  If(LookUp(Settings, Key=KeySolution & "/BlueLightCutMode").Value = "true", true, false)));
    If(IsBlank(DebugMode),         Set(DebugMode,         If(LookUp(Settings, Key=KeySolution & "/DebugMode").Value = "true", true, false)));
    If(IsBlank(MaintenanceMode),   Set(MaintenanceMode,   If(LookUp(Settings, Key=KeySolution & "/MaintenanceMode").Value = "true", true, false)));
    // If(IsBlank(AppId),          Set(AppId,             LookUp(Settings,    Key=KeySolution & "/Id").Value));
    If(IsBlank(AppVersion),        Set(AppVersion,        LookUp(Settings,    Key=KeySolution & "/Version").Value));
    
    Set(ContactsAppId, LookUp(Settings, Key="ContactsApp/Id").Value);

    Set(IsAppAdministrators, Office365ユーザー.MyProfileV2().userPrincipalName in AppAdministrators.Value);

    // リソースを取得する
    Set(SplushImageUrl, First(First(Filter(Resources, Key="Yakata/Image/Splush")).Attachments).Value);

    If(DebugMode, Log.Run(
        Text(Now(), UTC),
        LookUp(LogLevels, Name="Debug").Name,
        "Initialization started.",
        KeySolution,
        KeyApp,
        "",
        "App.OnStart",
        ""
    ));

    // アプリメタデータ読み出し
    Set(AppMetadata, 作成者向けPowerApps.GetApp(AppId));
    Set(AppMetadataProperties, AppMetadata.properties);
    Set(AppDate,
        AppMetadataProperties.appVersion & " " &
        AppMetadataProperties.lastModifiedTime & " " &
        AppMetadataProperties.lastModifiedBy.email & " " & 
        AppMetadataProperties.createdTime
    );

    If(DebugMode, Log.Run(
        Text(Now(), UTC),
        LookUp(LogLevels, Name="Debug").Name,
        "Initialization: Id:" & AppMetadata.id & " Type:" & AppMetadata.type &
            " Name: " & AppMetadata.name & " AppDate: " & AppDate,
        KeySolution,
        KeyApp,
        "",
        "App.OnStart",
        ""
    ));
    Set(AppName, AppMetadataProperties.displayName);

    // クライアント環境情報取得
    Set(clientInfo, ClientInfo.Run());
    Set(IpAddress, If(!IsBlank(clientInfo.ipaddress), clientInfo.ipaddress, "-"));
    Set(UserAgent, If(!IsBlank(clientInfo.useragent), clientInfo.useragent, "-"));
    Set(Lang, Language());
    If(DebugMode, Log.Run(
        Text(Now(), UTC),
        LookUp(LogLevels, Name="Debug").Name,
        "Initialization: IpAddress: " & IpAddress & " UserAgent: " & UserAgent & " Lang: " & Lang,
        KeySolution,
        KeyApp,
        "",
        "App.OnStart",
        ""
    ));

    // ログ書き込み
    If(DebugMode, Log.Run(
        Text(Now(), UTC),
        LookUp(LogLevels, Name="Debug").Name,
        "Initialization of App finished.",
        KeySolution,
        KeyApp,
        "",
        "App.OnStart",
        ""
    ));

    // 更新チェック
    Set(
        String_NewVerwionExists,
        If(
            !IsBlank(LookUp(Strings, And(Key="NewVerwionExists", Language=Language()), Value)),
            LookUp(Strings, And(Key="NewVerwionExists", Language=Language()), Value),
            LookUp(Strings, And(Key="NewVerwionExists", Language=""), Value)
        )
    );
    If(
        AppPublishedVersion <> AppVersion,
        Notify(String_NewVerwionExists, NotificationType.Information, 5000)
    );

    // ここまで初期化処理
    Set(AppInitialized, true);
);

// ルーティング
If(
    MaintenanceMode,
    Navigate(MaintenanceModeScreen, ScreenTransition.None)
);
// https://apps.powerapps.com/play/<app-id>?path=log
If(
    !IsBlank(Param("path")),

    If(Lower(Param("path"))="about",                          Navigate(AboutScreen,   ScreenTransition.None));
    If(Lower(Param("path"))="log" && IsAppAdministrators,     Navigate(LogScreen,     ScreenTransition.None));
    If(Lower(Param("path"))="main",                           Navigate(MainScreen,    ScreenTransition.None));
    If(Lower(Param("path"))="setting",                        Navigate(SettingScreen, ScreenTransition.None));
);
